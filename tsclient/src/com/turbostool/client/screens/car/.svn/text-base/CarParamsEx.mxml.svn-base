<?xml version="1.0" ?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:controls="com.turbostool.controls.*" >

    <mx:Script><![CDATA[
        import com.turbostool.client.model.CarInfo;
        import com.turbostool.client.model.ItemInfo;
        import com.turbostool.client.utils.Utils;

        private var powerColor: uint = 0xee2509;
        private var powerColorShaded: uint = 0x861505;

        private var speedColor: uint = 0x15c6ff;
        private var speedColorShaded: uint = 0x0b6784;

        private var controllabilityColor: uint = 0xf49602;
        private var controllabilityColorShaded: uint = 0x865201;

        private var brakingColor: uint = 0xc203ef;
        private var brakingColorShaded: uint = 0x6a0283;

        [Bindable]
        private var _power:Number = 0;

        [Bindable]
        private var _speed:Number = 0;

        [Bindable]
        private var _controllability:Number = 0;

        [Bindable]
        private var _breaking:Number = 0;

        [Bindable]
        private var _max:Number = 100;

        private var _carInfo:CarInfo;

        private var _currentUpgradeInfo: ItemInfo;
        private var _newUpgradeInfo: ItemInfo;

        [Bindable]
        public function get carInfo():CarInfo {
            return _carInfo;
        }

        public function set carInfo(val:CarInfo):void {
            _carInfo = val;

            recalcParams();
        }

        public function get newUpgradeInfo():ItemInfo {
            return _newUpgradeInfo;
        }

        public function set newUpgradeInfo(value:ItemInfo):void {
            _newUpgradeInfo = value;

            recalcParams();
        }

        public function get currentUpgradeInfo():ItemInfo {
            return _currentUpgradeInfo;
        }

        public function set currentUpgradeInfo(value:ItemInfo):void {
            _currentUpgradeInfo = value;

            recalcParams();
        }

        private function recalcParams(): void {
            if(_carInfo != null) {
                _power = Math.max(_carInfo.acceleration, _carInfo.power);
                _speed = Math.max(_carInfo.maxSpeed, _carInfo.speed);
                _controllability = Math.max(_carInfo.controllability, _carInfo.steering);
                _breaking = Math.max(_carInfo.breaking, 0);

                lbl5.text = "" + _carInfo.nitroCount;
            }

            var powerDelta: Number = 0;
            var speedDelta: Number = 0;
            var controllabilityDelta: Number = 0;
            var brakingDelta: Number = 0;

            lbl6.visible = false;
            
            if(_newUpgradeInfo != null && _newUpgradeInfo.usingType != "inventory") {
                powerDelta = _newUpgradeInfo.power - (_currentUpgradeInfo != null ? _currentUpgradeInfo.power : 0);
                speedDelta = _newUpgradeInfo.speed - (_currentUpgradeInfo != null ? _currentUpgradeInfo.speed : 0);
                controllabilityDelta = _newUpgradeInfo.controllability - (_currentUpgradeInfo != null ? _currentUpgradeInfo.controllability : 0);
                brakingDelta = _newUpgradeInfo.braking - (_currentUpgradeInfo != null ? _currentUpgradeInfo.braking : 0);
            } else if(_newUpgradeInfo != null && _newUpgradeInfo.usingType == "inventory") {
                lbl6.text = "+" + _newUpgradeInfo.usingCount;
                lbl6.visible = true;
            }

            setProgress(pr1, _power, powerDelta / 5, powerColor, powerColorShaded);
            setProgress(pr2, _speed, speedDelta / 5, speedColor, speedColorShaded);
            setProgress(pr3, _controllability, controllabilityDelta / 10, controllabilityColor, controllabilityColorShaded);
            setProgress(pr4, _breaking, brakingDelta / 2, brakingColor, brakingColorShaded);

            setLabel(lbl1, Utils.round(powerDelta / 5, 0));
            setLabel(lbl2, Utils.round(speedDelta / 5, 0));
            setLabel(lbl3, Utils.round(controllabilityDelta / 10, 0));
            setLabel(lbl4, Utils.round(brakingDelta / 2, 0));
        }

        private function setLabel(label: mx.controls.Label, delta: Number): void {
            var str: String = "" + delta;
            if(delta > 0)
                str = "+" + str;

            if(delta != 0)
                label.visible = true;
            else
                label.visible = false;

            if(delta > 0)
                label.setStyle("color", 0xd4880e);
            else
                label.setStyle("color", 0xe01900);

            label.text = str;
        }

        private function setProgress(pr: ProgressBarEx, param: Number, delta:Number, color: uint, shadedColor: uint):void {
            if(delta < 0) {
                pr.barColor1 = shadedColor;
                pr.barColor2 = color;
                pr.progress1 = param;
                pr.progress2 = param + delta;
            } else {
                pr.barColor1 = 0xFFFFFF;
                pr.barColor2 = color;
                pr.progress1 = param + delta;
                pr.progress2 = param;
            }
        }

        public function set max(val:Number):void {
            _max = val;
        }

        public function set power(val:Number):void {
            _power = val;
        }

        public function set speed(val:Number):void {
            _speed = val;
        }

        public function set controllability(val:Number):void {
            _controllability = val;
        }

        public function set breaking(val:Number):void {
            _breaking = val;
        }

        private function str(name: String, params: * = null): String {
            return Client.instance.str(name, params);
        }

        ]]></mx:Script>

    <mx:Label x="0" y="0" text="{str('power')}" fontWeight="bold" color="#FEFFFF" fontFamily="Arial" height="16" alpha="0.6" />
    <mx:Label x="0" y="12" text="{str('speed')}" fontWeight="bold" color="#FEFFFF" fontFamily="Arial" height="16" alpha="0.6" />
    <mx:Label x="0" y="24" text="{str('control')}" fontWeight="bold" color="#FEFFFF" fontFamily="Arial" height="16" alpha="0.6" />
    <mx:Label x="0" y="36" text="{str('brakes')}" fontWeight="bold" color="#FEFFFF" fontFamily="Arial" height="16" alpha="0.6" />
    <mx:Label x="0" y="48" text="{str('nitro2')}" fontWeight="bold" color="#FEFFFF" fontFamily="Arial" height="16" alpha="0.6" />

    <controls:ProgressBarEx id="pr1" left="105" y="3" height="10" right="1"
                    minValue="0" maxValue="100" borderWidth="1" borderColor="#FFFFFF" />
    
    <controls:ProgressBarEx id="pr2" left="105" y="15" height="10" right="1"
                    minValue="0" maxValue="100" borderWidth="1" borderColor="#FFFFFF" />

    <controls:ProgressBarEx id="pr3" left="105" y="27" height="10" right="1"
                    minValue="0" maxValue="100" borderWidth="1" borderColor="#FFFFFF" />

    <controls:ProgressBarEx id="pr4" left="105" y="39" height="10" right="1"
                    minValue="0" maxValue="100" borderWidth="1" borderColor="#FFFFFF" />

    <mx:Label id="lbl1" x="74" y="0"  text="+800" fontFamily="Arial" width="31" textAlign="right" color="#D4880E"/>
    <mx:Label id="lbl2" x="74" y="12" text="+800" fontFamily="Arial" width="31" textAlign="right" color="#D4880E"/>
    <mx:Label id="lbl3" x="74" y="24" text="+800" fontFamily="Arial" width="31" textAlign="right" color="#D4880E"/>
    <mx:Label id="lbl4" x="74" y="36" text="+800" fontFamily="Arial" width="31" textAlign="right" color="#D4880E"/>
    <mx:Label id="lbl5" x="74" y="48" text="+800" fontFamily="Arial" width="31" textAlign="right" color="#FFFFFF" alpha="0.6"/>
    <mx:Label id="lbl6" x="104" y="48" text="+800" fontFamily="Arial" color="#D4880E"/>

</mx:Canvas>